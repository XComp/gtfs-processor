buildscript {
    ext {
        javaVersion = '1.8'
        springBootVersion = '2.2.6.RELEASE'
        junitVersion = '5.6.1'
    }
}

plugins {
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version '1.0.8.RELEASE' apply false
    id 'com.avast.gradle.docker-compose' version '0.9.4'
}

apply plugin: 'idea'

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

subprojects {
    if (!name.contains("generic")) {
        apply plugin: 'org.springframework.boot'
    }

    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    configurations {
        developmentOnly
        runtimeClasspath.extendsFrom developmentOnly
        compileOnly.extendsFrom annotationProcessor
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR1"
        }

        dependencies {
            dependency group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
            dependency group: 'com.google.guava', name: 'guava', version: '29.0-jre'
            dependency group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
            dependency group: 'info.picocli', name: 'picocli', version: '4.2.0'
        }
    }

    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    }

    test {
        useJUnitPlatform()
    }
}

dockerCompose {
    // captureContainersOutput = true
    captureContainersOutput = false
    stopContainers = true
    removeContainers = true
    removeImages = "Local"
    removeVolumes = true
    removeOrphans = true
    forceRecreate = true
    waitForTcpPorts = false
    projectName = project.name
}

project.subprojects.each { subProject ->
    subProject.test.dependsOn composeUp
    subProject.test.finalizedBy composeDown
}
